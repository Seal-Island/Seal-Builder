buildscript {
    repositories {
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven' }
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

group = 'com.focamacho'
version = '1.0'

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'net.minecraftforge.gradle'

    group = project.group
    version = project.version

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    minecraft {
        mappings channel: 'stable', version: '39-1.12'
    }

    repositories {
        mavenCentral()
        maven { url = 'https://repo.focamacho.com/repository/releases/' }
    }

    dependencies {
        //Forge
        minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

        //Seal Library Common
        compileOnly 'com.focamacho:seallibrary:1.0:common'

        //Pixelmon
        compileOnly files(project.rootDir.toString() + "/libs/Pixelmon-1.12.2-8.1.2-universal.jar")
    }

    tasks.withType(JavaCompile) {
        options.encoding("UTF-8")
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path)}

task allJar(type: Jar, dependsOn: subprojects.build) {
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}